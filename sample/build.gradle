apply plugin: 'com.android.application'

// Changes the base apk name to EtsyBlurDemo
project.ext.set("archivesBaseName", "EtsyBlurDemo");

android {
    compileSdkVersion project.ANDROID_COMPILE_SDK_VERSION
    buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION

    defaultConfig {
        applicationId "com.ms.square.android.etsyblurdemo"
        minSdkVersion project.ANDROID_MIN_SDK_VERSION
        targetSdkVersion project.ANDROID_TARGET_SDK_VERSION
        versionCode 6
        versionName "0.2.1"
        renderscriptTargetApi project.ANDROID_RS_TARGET_VERSION
        renderscriptSupportModeEnabled true
    }

    signingConfigs {
        release {
            def Properties keyProps = new Properties()
            // double check if keystore.properties exists to avoid exception
            if (file("../keystore.properties").exists()) {
                keyProps.load(new FileInputStream(file('../keystore.properties')))
            }
            storeFile keyProps["storeFile"] != null ? file(keyProps["storeFile"]) : null
            storePassword keyProps["storePassword"]
            keyAlias keyProps["keyAlias"]
            keyPassword keyProps["keyPassword"]
        }
    }

    buildTypes {
        release {
            zipAlignEnabled true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            // check if keystore.properties exists in the root
            if (file("../keystore.properties").exists()) {
                signingConfig signingConfigs.release
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.android.support:appcompat-v7:${supportPackageVersion}"
    // for design support library
    compile "com.android.support:design:${project.supportPackageVersion}"

    compile project(':lib')
}